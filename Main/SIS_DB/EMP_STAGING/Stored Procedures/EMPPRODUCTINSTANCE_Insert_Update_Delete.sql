CREATE PROCEDURE EMP_STAGING.EMPPRODUCTINSTANCE_Insert_Update_Delete(@FORCE_LOAD BIT = 'FALSE'
                                                                             ,@DEBUG BIT = 'FALSE')
AS
BEGIN
        SET TRANSACTION ISOLATION LEVEL SNAPSHOT;
        SET XACT_ABORT,NOCOUNT ON;
        BEGIN TRY
		DECLARE @SCHEMANAME sysname = 'EMP_STAGING'
			   ,@TABLENAME  sysname = 'EMPPRODUCTINSTANCE';

		DECLARE @FULLTABLENAME   sysname = @SCHEMANAME + '.' + @TABLENAME
			   ,@CURRENT_VERSION BIGINT  = CHANGE_TRACKING_CURRENT_VERSION();

		DECLARE @LAST_SYNCHRONIZATION_VERSION BIGINT           = EMP_STAGING._getLastSynchVersion (@FULLTABLENAME)
			   ,@RESULT                       BIT
			   ,@ROWCOUNT                     BIGINT
			   ,@MERGED_ROWS                  BIGINT           = 0
			   ,@SYSUTCDATETIME               DATETIME2(6)     = SYSUTCDATETIME()
			   ,@PROCNAME                     VARCHAR(200)     = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
			   ,@PROCESSID                    UNIQUEIDENTIFIER = NEWID()
			   ,@LOGMESSAGE                   VARCHAR(MAX);

		DECLARE @MERGE_RESULTS TABLE (ACTIONTYPE              NVARCHAR(10)
									 ,[EMPPRODUCTINSTANCE_ID] INT                NOT NULL
                                     ,[EMPSALESMODEL_ID]      INT                NOT NULL
                                     ,[NUMBER]                VARCHAR (60)       NOT NULL
                                     ,[NAME]                  VARCHAR (60)       NOT NULL
                                     ,[ALIAS1]                VARCHAR (60)           NULL
                                     ,[ALIAS2]                VARCHAR (60)           NULL
                                     ,[ALIAS3]                VARCHAR (60)           NULL
                                     ,[ALTERNATESERIALNUMBER] VARCHAR (60)           NULL
                                     ,[NUMERICVALUE]          VARCHAR (10)           NULL
                                     ,[NUMERICVALUE2]         INT                    NULL
                                     ,[SALESORDERNUMBER]      VARCHAR (60)           NULL
                                     ,[SERVICEABLE]           BIT                    NULL
                                     ,[VERSION]               VARCHAR (13)            NULL
                                     ,[MODEL]                 VARCHAR (60)           NULL
                                     ,[SNP]                   VARCHAR (60)           NULL
                                     ,[STATE]                 VARCHAR (15)           NULL
                                     ,[CURRENTSTATE]          VARCHAR (15)           NULL
                                     ,[LASTMODIFIEDDATE]      DATETIME2 (6)          NULL);

/*
	Davide 20200205
	in order to avoid deadlocks errors like: "Transaction (Process ID 246) was deadlocked on lock | communication buffer resources with another process and has been chosen as the deadlock victim. Rerun the transaction.",
	we move the creation of temp table outside the TRANSACTION block, even if it is not extremely elegant
*/
        DROP TABLE IF EXISTS #DIFF_PRIMARY_KEYS;
        SELECT CT.SYS_CHANGE_OPERATION AS OPERATION, CT.EMPPRODUCTINSTANCE_ID AS EMPPRODUCTINSTANCE_ID, CT.SYS_CHANGE_VERSION AS SYS_CHANGE_VERSION
        INTO #DIFF_PRIMARY_KEYS
        FROM CHANGETABLE(CHANGES EMP_STAGING.EMPPRODUCTINSTANCE, @LAST_SYNCHRONIZATION_VERSION) AS CT;

        BEGIN TRANSACTION;
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution started',@DATAVALUE = NULL;

		IF @FORCE_LOAD = 'FALSE'
        BEGIN
            EXEC SISWEB_OWNER_STAGING._getFullOrDiffEMP @SCHEMANAME = @SCHEMANAME,@TABLENAME = @TABLENAME,@CURRENT_VERSION = @CURRENT_VERSION,@DEBUG = @DEBUG,@RESULT = @RESULT OUTPUT;

		    IF @DEBUG = 'TRUE'
				PRINT FORMATMESSAGE('@RESULT=%s',IIF(@RESULT = 1,'TRUE','FALSE'));

			IF @RESULT = 'TRUE'
            BEGIN
				-- 6. Obtain the changes

				IF @DEBUG = 'TRUE'
                    SELECT * FROM #DIFF_PRIMARY_KEYS AS DK;

-- 7.a Delete if needed
                SELECT @ROWCOUNT = COUNT(*) FROM #DIFF_PRIMARY_KEYS AS DK WHERE DK.OPERATION = 'D';
                IF @ROWCOUNT > 0
                BEGIN
                    DELETE FROM SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE
                    OUTPUT 'DELETE' ACTIONTYPE,DELETED.EMPPRODUCTINSTANCE_ID,DELETED.EMPSALESMODEL_ID,DELETED.NUMBER,DELETED.NAME,DELETED.ALIAS1,DELETED.ALIAS2,
                    DELETED.ALIAS3,DELETED.ALTERNATESERIALNUMBER,DELETED.NUMERICVALUE,DELETED.NUMERICVALUE2,DELETED.SALESORDERNUMBER,DELETED.SERVICEABLE,
                    DELETED.VERSION,DELETED.MODEL,DELETED.SNP,DELETED.STATE,DELETED.CURRENTSTATE,DELETED.LASTMODIFIEDDATE
                    INTO @MERGE_RESULTS
                    WHERE EXISTS
                    (
                        SELECT *
                        FROM #DIFF_PRIMARY_KEYS AS DK
                        WHERE DK.EMPPRODUCTINSTANCE_ID     = EMPPRODUCTINSTANCE.EMPPRODUCTINSTANCE_ID AND
                              DK.OPERATION = 'D'
                    );

                    SELECT @MERGED_ROWS = @@ROWCOUNT;

                    SET @LOGMESSAGE = FORMATMESSAGE('Deleted %s rows',CAST(@MERGED_ROWS AS VARCHAR(9)));
                    EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
                END;

				-- 7.b Insert if needed
                SELECT @ROWCOUNT = COUNT(*) FROM #DIFF_PRIMARY_KEYS AS DK WHERE DK.OPERATION = 'I';
                IF @ROWCOUNT > 0
                BEGIN
                    INSERT INTO SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE (EMPPRODUCTINSTANCE_ID,EMPSALESMODEL_ID,NUMBER,NAME,ALIAS1,ALIAS2,ALIAS3,ALTERNATESERIALNUMBER,
                                                                        NUMERICVALUE,NUMERICVALUE2,SALESORDERNUMBER,SERVICEABLE,VERSION,MODEL,SNP,
                                                                        STATE,CURRENTSTATE,LASTMODIFIEDDATE)
                    OUTPUT 'INSERT' ACTIONTYPE,INSERTED.EMPPRODUCTINSTANCE_ID,INSERTED.EMPSALESMODEL_ID,INSERTED.NUMBER,INSERTED.NAME,INSERTED.ALIAS1,INSERTED.ALIAS2,
                    INSERTED.ALIAS3,INSERTED.ALTERNATESERIALNUMBER,INSERTED.NUMERICVALUE,INSERTED.NUMERICVALUE2,INSERTED.SALESORDERNUMBER,INSERTED.SERVICEABLE,
                    INSERTED.VERSION,INSERTED.MODEL,INSERTED.SNP,INSERTED.STATE,INSERTED.CURRENTSTATE,INSERTED.LASTMODIFIEDDATE
						    INTO @MERGE_RESULTS
                            SELECT EMPPRODUCTINSTANCE_ID,EMPSALESMODEL_ID,NUMBER,NAME,ALIAS1,ALIAS2,ALIAS3,ALTERNATESERIALNUMBER,
                                   NUMERICVALUE,NUMERICVALUE2,SALESORDERNUMBER,SERVICEABLE,VERSION,MODEL,SNP,
                                   STATE,STATE,LASTMODIFIEDDATE
                            FROM EMP_STAGING.EMPPRODUCTINSTANCE AS DT
                            WHERE EXISTS
                                      (
                                          SELECT *
                                          FROM #DIFF_PRIMARY_KEYS AS DK
                                          WHERE DK.EMPPRODUCTINSTANCE_ID = DT.EMPPRODUCTINSTANCE_ID
                                          AND DK.OPERATION = 'I'
                                      );

                    SELECT @MERGED_ROWS = @@ROWCOUNT;

                    SET @LOGMESSAGE = FORMATMESSAGE('Inserted %s rows',CAST(@MERGED_ROWS AS VARCHAR(9)));
                    EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
                END;

				-- 7.c Update if needed
                SELECT @ROWCOUNT = COUNT(*) FROM #DIFF_PRIMARY_KEYS AS DK WHERE DK.OPERATION = 'U';
                IF @ROWCOUNT > 0
                BEGIN
                    UPDATE SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE
                    SET EMPSALESMODEL_ID = ST.EMPSALESMODEL_ID,NUMBER = ST.NUMBER,NAME = ST.NAME,ALIAS1 = ST.ALIAS1,
                        ALIAS2 = ST.ALIAS2,ALIAS3 = ST.ALIAS3,ALTERNATESERIALNUMBER = ST.ALTERNATESERIALNUMBER,
                        NUMERICVALUE = ST.NUMERICVALUE,NUMERICVALUE2 = ST.NUMERICVALUE2,SALESORDERNUMBER = ST.SALESORDERNUMBER,
                        SERVICEABLE = ST.SERVICEABLE,VERSION = ST.VERSION,MODEL = ST.MODEL,SNP = ST.SNP,STATE = ST.STATE,
                        CURRENTSTATE = ST.STATE,LASTMODIFIEDDATE = ST.LASTMODIFIEDDATE
                    OUTPUT 'UPDATE' ACTIONTYPE,INSERTED.EMPPRODUCTINSTANCE_ID,INSERTED.EMPSALESMODEL_ID,INSERTED.NUMBER,INSERTED.NAME,INSERTED.ALIAS1,INSERTED.ALIAS2,
                    INSERTED.ALIAS3,INSERTED.ALTERNATESERIALNUMBER,INSERTED.NUMERICVALUE,INSERTED.NUMERICVALUE2,INSERTED.SALESORDERNUMBER,INSERTED.SERVICEABLE,
                    INSERTED.VERSION,INSERTED.MODEL,INSERTED.SNP,INSERTED.STATE,INSERTED.CURRENTSTATE,INSERTED.LASTMODIFIEDDATE
                    INTO @MERGE_RESULTS
                    FROM EMP_STAGING.EMPPRODUCTINSTANCE AS ST
                        JOIN SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE AS TT ON ST.EMPPRODUCTINSTANCE_ID = TT.EMPPRODUCTINSTANCE_ID
                    WHERE EXISTS
                    (
                        SELECT *
                        FROM #DIFF_PRIMARY_KEYS AS DK
                        WHERE DK.EMPPRODUCTINSTANCE_ID = ST.EMPPRODUCTINSTANCE_ID AND
                              DK.OPERATION = 'U'
                    );
                    SELECT @MERGED_ROWS = @@ROWCOUNT;

                    SET @LOGMESSAGE = FORMATMESSAGE('Updated %s rows',CAST(@MERGED_ROWS AS VARCHAR(9)));
                    EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
                END;

				-- 8. Store current version to be used the next time
				IF @DEBUG = 'TRUE'
                    SELECT * FROM @MERGE_RESULTS AS MR;

                EXEC EMP_STAGING._setLastSynchVersion @FULLTABLENAME,@CURRENT_VERSION;

				SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',
				(
					SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE =
					'UPDATE'
					) AS Updated,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted,
					(
						SELECT MR.EMPPRODUCTINSTANCE_ID,MR.EMPSALESMODEL_ID,MR.NUMBER,MR.NAME,MR.ALIAS1,MR.ALIAS2,
                               MR.ALIAS3,MR.ALTERNATESERIALNUMBER,MR.NUMERICVALUE,MR.NUMERICVALUE2,
                               MR.SALESORDERNUMBER,MR.SERVICEABLE,MR.VERSION,MR.MODEL,MR.SNP,MR.STATE,MR.CURRENTSTATE,MR.LASTMODIFIEDDATE
						FROM @MERGE_RESULTS AS MR FOR JSON AUTO
					) AS Modified_Rows FOR JSON PATH,WITHOUT_ARRAY_WRAPPER
				),'Modified Rows');
                EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
            END;
        END;

		/* to be done: handle full load with MERGE */
		IF @FORCE_LOAD = 'TRUE'
		--   OR @MODIFIED_ROWS_PERCENTAGE BETWEEN-0.10 AND 0.10
        BEGIN
			SET @LOGMESSAGE = 'Executing load: Force Load = TRUE';
            EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @FORCE_LOAD;

			/* MERGE command */
            MERGE INTO SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE tgt
            USING EMP_STAGING.EMPPRODUCTINSTANCE src
            ON src.EMPPRODUCTINSTANCE_ID = tgt.EMPPRODUCTINSTANCE_ID
            WHEN MATCHED AND EXISTS
            (
                SELECT src.EMPSALESMODEL_ID,src.NUMBER,src.NAME,src.ALIAS1,src.ALIAS2,
                       src.ALIAS3,src.ALTERNATESERIALNUMBER,src.NUMERICVALUE,src.NUMERICVALUE2,
                       src.SALESORDERNUMBER,src.SERVICEABLE,src.VERSION,src.MODEL,src.SNP,src.STATE,src.STATE,src.LASTMODIFIEDDATE
                    EXCEPT
                SELECT tgt.EMPSALESMODEL_ID,tgt.NUMBER,tgt.NAME,tgt.ALIAS1,tgt.ALIAS2,
                       tgt.ALIAS3,tgt.ALTERNATESERIALNUMBER,tgt.NUMERICVALUE,tgt.NUMERICVALUE2,
                       tgt.SALESORDERNUMBER,tgt.SERVICEABLE,tgt.VERSION,tgt.MODEL,tgt.SNP,tgt.STATE,tgt.CURRENTSTATE,tgt.LASTMODIFIEDDATE
            )
                THEN UPDATE SET tgt.EMPSALESMODEL_ID = src.EMPSALESMODEL_ID,tgt.NUMBER = src.NUMBER,tgt.NAME=src.NAME,
                                tgt.ALIAS1 = src.ALIAS1,tgt.ALIAS2 = src.ALIAS2,tgt.ALIAS3 = src.ALIAS3,
                                tgt.ALTERNATESERIALNUMBER = src.ALTERNATESERIALNUMBER,tgt.NUMERICVALUE = src.NUMERICVALUE,
                                tgt.NUMERICVALUE2 = src.NUMERICVALUE2,tgt.SALESORDERNUMBER = src.SALESORDERNUMBER,
                                tgt.SERVICEABLE = src.SERVICEABLE,tgt.VERSION = src.VERSION,tgt.MODEL = src.MODEL,
                                tgt.SNP = src.SNP,tgt.STATE = src.STATE,tgt.CURRENTSTATE = src.STATE,tgt.LASTMODIFIEDDATE = src.LASTMODIFIEDDATE
            WHEN NOT MATCHED BY TARGET
                THEN
                INSERT(EMPPRODUCTINSTANCE_ID,EMPSALESMODEL_ID,NUMBER,NAME,ALIAS1,ALIAS2,ALIAS3,ALTERNATESERIALNUMBER,NUMERICVALUE,
                      NUMERICVALUE2,SALESORDERNUMBER,SERVICEABLE,VERSION,MODEL,SNP,STATE,CURRENTSTATE,LASTMODIFIEDDATE)
                VALUES (src.EMPPRODUCTINSTANCE_ID,src.EMPSALESMODEL_ID,src.NUMBER,src.NAME,src.ALIAS1,src.ALIAS2,
                        src.ALIAS3,src.ALTERNATESERIALNUMBER,src.NUMERICVALUE,src.NUMERICVALUE2,
                        src.SALESORDERNUMBER,src.SERVICEABLE,src.VERSION,src.MODEL,src.SNP,src.STATE,src.STATE,src.LASTMODIFIEDDATE)
                WHEN NOT MATCHED BY SOURCE
                THEN DELETE
            OUTPUT $ACTION,COALESCE(inserted.EMPPRODUCTINSTANCE_ID,deleted.EMPPRODUCTINSTANCE_ID) EMPPRODUCTINSTANCE_ID,COALESCE(inserted.EMPSALESMODEL_ID,deleted.EMPSALESMODEL_ID) EMPSALESMODEL_ID,
			COALESCE(inserted.NUMBER,deleted.NUMBER) NUMBER,COALESCE(inserted.NAME,deleted.NAME) NAME,
            COALESCE(inserted.ALIAS1,deleted.ALIAS1) ALIAS1,COALESCE(inserted.ALIAS2,deleted.ALIAS2) ALIAS2,
            COALESCE(inserted.ALIAS3,deleted.ALIAS3) ALIAS3,COALESCE(inserted.ALTERNATESERIALNUMBER,deleted.ALTERNATESERIALNUMBER) ALTERNATESERIALNUMBER,
            COALESCE(inserted.NUMERICVALUE,deleted.NUMERICVALUE) NUMERICVALUE,COALESCE(inserted.NUMERICVALUE2,deleted.NUMERICVALUE2) NUMERICVALUE2,
			COALESCE(inserted.SALESORDERNUMBER,deleted.SALESORDERNUMBER) SALESORDERNUMBER,COALESCE(inserted.SERVICEABLE,deleted.SERVICEABLE) SERVICEABLE,
            COALESCE(inserted.VERSION,deleted.VERSION) VERSION,COALESCE(inserted.MODEL,deleted.MODEL) MODEL,
            COALESCE(inserted.SNP,deleted.SNP) SNP,COALESCE(inserted.STATE,deleted.STATE) STATE,COALESCE(inserted.CURRENTSTATE,deleted.CURRENTSTATE) CURRENTSTATE,COALESCE(inserted.LASTMODIFIEDDATE,deleted.LASTMODIFIEDDATE) LASTMODIFIEDDATE
				   INTO @MERGE_RESULTS;

			/* MERGE command */
            SELECT @MERGED_ROWS = @@ROWCOUNT;

            EXEC EMP_STAGING._setLastSynchVersion @FULLTABLENAME,@CURRENT_VERSION;

			SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',
			(
				SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE =
				'UPDATE'
				) AS Updated,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted,
				(
					SELECT MR.ACTIONTYPE,MR.EMPPRODUCTINSTANCE_ID,MR.EMPSALESMODEL_ID,MR.NUMBER,MR.NAME,MR.ALIAS1,MR.ALIAS2,
                           MR.ALIAS3,MR.ALTERNATESERIALNUMBER,MR.NUMERICVALUE,MR.NUMERICVALUE2,
                           MR.SALESORDERNUMBER,MR.SERVICEABLE,MR.VERSION,MR.MODEL,MR.SNP,MR.STATE,MR.CURRENTSTATE,MR.LASTMODIFIEDDATE
					FROM @MERGE_RESULTS AS MR FOR JSON AUTO
				) AS Modified_Rows FOR JSON PATH,WITHOUT_ARRAY_WRAPPER
			),'Modified Rows');
            EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
        END;

        COMMIT;
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;
    END TRY
    BEGIN CATCH
		DECLARE @ERRORMESSAGE NVARCHAR(4000) = ERROR_MESSAGE()
			   ,@ERRORLINE    INT            = ERROR_LINE()
			   ,@ERRORNUM     INT            = ERROR_NUMBER();
		IF @@TRANCOUNT > 0
			ROLLBACK TRANSACTION;
		SET @LOGMESSAGE = FORMATMESSAGE('LINE %s: %s',CAST(@ERRORLINE AS VARCHAR(10)),CAST(@ERRORMESSAGE AS VARCHAR(4000)));
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @ERRORNUM;
        THROW;
    END CATCH;
END;
GO

