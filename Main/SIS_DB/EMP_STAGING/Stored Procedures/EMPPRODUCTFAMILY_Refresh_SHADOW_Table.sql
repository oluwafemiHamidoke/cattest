CREATE PROCEDURE EMP_STAGING.EMPPRODUCTFAMILY_Refresh_SHADOW_Table (@DEBUG BIT = 'FALSE')
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL SNAPSHOT;
	SET XACT_ABORT,NOCOUNT ON;
	BEGIN TRY

		DECLARE @MERGED_ROWS BIGINT           = 0
			   ,@PROCNAME    VARCHAR(200)     = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
			   ,@PROCESSID   UNIQUEIDENTIFIER = NEWID()
			   ,@LOGMESSAGE  VARCHAR(MAX);

		DECLARE @MERGE_RESULTS TABLE (ACTIONTYPE            NVARCHAR(10),
									 [EMPPRODUCTFAMILY_ID]  INT           NOT NULL,
                                     [NAME]                 VARCHAR (60),
                                     [NUMBER]               VARCHAR (15)  NOT NULL,
                                     [VERSION]              VARCHAR (13),
                                     [STATE]                VARCHAR (15),
                                     [CURRENTSTATE]         VARCHAR (15),
                                     [LASTMODIFIEDDATE]     DATETIME2 (6));

		BEGIN TRANSACTION;
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution started',@DATAVALUE = NULL;

		/* MERGE command */
		MERGE INTO SISWEB_OWNER_SHADOW.EMPPRODUCTFAMILY tgt
		USING SISWEB_OWNER.EMPPRODUCTFAMILY src
        ON src.EMPPRODUCTFAMILY_ID = tgt.EMPPRODUCTFAMILY_ID
		WHEN MATCHED AND EXISTS
        (
            SELECT src.NAME,src.NUMBER,src.VERSION,src.STATE,src.CURRENTSTATE,src.LASTMODIFIEDDATE
                EXCEPT
            SELECT tgt.NAME,tgt.NUMBER,tgt.VERSION,tgt.STATE,tgt.CURRENTSTATE,tgt.LASTMODIFIEDDATE
        )
        THEN UPDATE SET tgt.NAME = src.NAME,tgt.NUMBER = src.NUMBER,tgt.VERSION = src.VERSION,tgt.STATE = src.STATE,
            tgt.CURRENTSTATE = src.CURRENTSTATE,tgt.LASTMODIFIEDDATE = src.LASTMODIFIEDDATE
		WHEN NOT MATCHED BY TARGET
            THEN
            INSERT(EMPPRODUCTFAMILY_ID,NAME,NUMBER,VERSION,STATE,CURRENTSTATE,LASTMODIFIEDDATE)
            VALUES (src.EMPPRODUCTFAMILY_ID,src.NAME,src.NUMBER,src.VERSION,src.STATE,src.CURRENTSTATE,src.LASTMODIFIEDDATE)
		WHEN NOT MATCHED BY SOURCE
        THEN DELETE
        OUTPUT $ACTION,COALESCE(inserted.EMPPRODUCTFAMILY_ID,deleted.EMPPRODUCTFAMILY_ID) EMPPRODUCTFAMILY_ID,COALESCE(inserted.NAME,deleted.NAME) NAME,
			COALESCE(inserted.NUMBER,deleted.NUMBER) NUMBER,COALESCE(inserted.VERSION,deleted.VERSION) VERSION,
            COALESCE(inserted.STATE,deleted.STATE) STATE,COALESCE(inserted.CURRENTSTATE,deleted.CURRENTSTATE) CURRENTSTATE,
            COALESCE(inserted.LASTMODIFIEDDATE,deleted.LASTMODIFIEDDATE) LASTMODIFIEDDATE
		INTO @MERGE_RESULTS;

		/* MERGE command */
		SELECT @MERGED_ROWS = @@ROWCOUNT;

        SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',
			(
				SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE =
				'UPDATE'
				) AS Updated,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted,
				(
					SELECT MR.ACTIONTYPE,MR.EMPPRODUCTFAMILY_ID,MR.NAME,MR.NUMBER,MR.VERSION,MR.STATE,MR.CURRENTSTATE,MR.LASTMODIFIEDDATE
					FROM @MERGE_RESULTS AS MR FOR JSON AUTO
				) AS Modified_Rows FOR JSON PATH,WITHOUT_ARRAY_WRAPPER
			),'Modified Rows');
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;

		COMMIT;
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;
	END TRY
	BEGIN CATCH
		DECLARE @ERRORMESSAGE NVARCHAR(4000) = ERROR_MESSAGE()
			   ,@ERRORLINE    INT            = ERROR_LINE()
			   ,@ERRORNUM     INT            = ERROR_NUMBER();
		IF @@TRANCOUNT > 0
			ROLLBACK TRANSACTION;
		SET @LOGMESSAGE = FORMATMESSAGE('LINE %s: %s',CAST(@ERRORLINE AS VARCHAR(10)),CAST(@ERRORMESSAGE AS VARCHAR(4000)));
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @ERRORNUM;
	END CATCH;
END;