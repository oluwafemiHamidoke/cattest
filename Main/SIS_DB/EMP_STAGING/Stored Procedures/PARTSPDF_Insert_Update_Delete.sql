CREATE PROCEDURE EMP_STAGING.PARTSPDF_Insert_Update_Delete(@FORCE_LOAD BIT = 'FALSE'
                                                          ,@DEBUG BIT = 'FALSE')
AS
BEGIN
        SET TRANSACTION ISOLATION LEVEL SNAPSHOT;
        SET XACT_ABORT,NOCOUNT ON;
        BEGIN TRY
		DECLARE @SCHEMANAME sysname = 'EMP_STAGING'
			   ,@TABLENAME  sysname = 'PARTSPDF';

		DECLARE @FULLTABLENAME   sysname = @SCHEMANAME + '.' + @TABLENAME
			   ,@CURRENT_VERSION BIGINT  = CHANGE_TRACKING_CURRENT_VERSION();

		DECLARE @LAST_SYNCHRONIZATION_VERSION BIGINT           = EMP_STAGING._getLastSynchVersion (@FULLTABLENAME)
			   ,@RESULT                       BIT
			   ,@ROWCOUNT                     BIGINT
			   ,@MERGED_ROWS                  BIGINT           = 0
			   ,@SYSUTCDATETIME               DATETIME2(6)     = SYSUTCDATETIME()
			   ,@PROCNAME                     VARCHAR(200)     = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
			   ,@PROCESSID                    UNIQUEIDENTIFIER = NEWID()
			   ,@LOGMESSAGE                   VARCHAR(MAX);

		DECLARE @MERGE_RESULTS TABLE (ACTIONTYPE            NVARCHAR(10)
									 ,PARTSPDF_ID           INT                    NOT NULL
                                     ,MEDIANUMBER           VARCHAR (8)            NOT NULL
									 ,PDFNUMBER             VARCHAR (60)           NOT NULL
									 ,PDFFILENAME           VARCHAR (60)           NOT NULL
									 ,PDFTYPE               VARCHAR (30)           NOT NULL
									 ,REVISION              INT                    NOT NULL
									 ,LANGUAGEINDICATOR     VARCHAR (2)            NOT NULL
                                     ,FILEPATH              VARCHAR (512)          NOT NULL
                                     ,FILESIZE              INT                    NOT NULL
		                             ,LASTMODIFIEDDATE      DATETIME               NULL);

/*
	Davide 20200205
	in order to avoid deadlocks errors like: "Transaction (Process ID 246) was deadlocked on lock | communication buffer resources with another process and has been chosen as the deadlock victim. Rerun the transaction.",
	we move the creation of temp table outside the TRANSACTION block, even if it is not extremely elegant
*/
        DROP TABLE IF EXISTS #DIFF_PRIMARY_KEYS;
        SELECT CT.SYS_CHANGE_OPERATION AS OPERATION, CT.PARTSPDF_ID AS PARTSPDF_ID, CT.SYS_CHANGE_VERSION AS SYS_CHANGE_VERSION
        INTO #DIFF_PRIMARY_KEYS
        FROM CHANGETABLE(CHANGES EMP_STAGING.PARTSPDF, @LAST_SYNCHRONIZATION_VERSION) AS CT;

        BEGIN TRANSACTION;
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution started',@DATAVALUE = NULL;

		IF @FORCE_LOAD = 'FALSE'
        BEGIN
            EXEC SISWEB_OWNER_STAGING._getFullOrDiffEMP @SCHEMANAME = @SCHEMANAME,@TABLENAME = @TABLENAME,@CURRENT_VERSION = @CURRENT_VERSION,@DEBUG = @DEBUG,@RESULT = @RESULT OUTPUT;

		    IF @DEBUG = 'TRUE'
				PRINT FORMATMESSAGE('@RESULT=%s',IIF(@RESULT = 1,'TRUE','FALSE'));

			IF @RESULT = 'TRUE'
            BEGIN
				-- 6. Obtain the changes

				IF @DEBUG = 'TRUE'
                    SELECT * FROM #DIFF_PRIMARY_KEYS AS DK;

-- 7.a Delete if needed
                SELECT @ROWCOUNT = COUNT(*) FROM #DIFF_PRIMARY_KEYS AS DK WHERE DK.OPERATION = 'D';
                IF @ROWCOUNT > 0
                BEGIN
                    DELETE FROM SISWEB_OWNER_SHADOW.PARTSPDF
                    OUTPUT 'DELETE' ACTIONTYPE,DELETED.PARTSPDF_ID,DELETED.MEDIANUMBER,DELETED.PDFNUMBER,DELETED.PDFFILENAME,DELETED.PDFTYPE,DELETED.REVISION,DELETED.LANGUAGEINDICATOR,
                    DELETED.FILEPATH,DELETED.FILESIZE,DELETED.LASTMODIFIEDDATE
                    INTO @MERGE_RESULTS
                    WHERE EXISTS
                    (
                        SELECT *
                        FROM #DIFF_PRIMARY_KEYS AS DK
                        WHERE DK.PARTSPDF_ID = PARTSPDF.PARTSPDF_ID AND
                              DK.OPERATION = 'D'
                    );

                    SELECT @MERGED_ROWS = @@ROWCOUNT;

                    SET @LOGMESSAGE = FORMATMESSAGE('Deleted %s rows',CAST(@MERGED_ROWS AS VARCHAR(9)));
                    EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
                END;

				-- 7.b Insert if needed
                SELECT @ROWCOUNT = COUNT(*) FROM #DIFF_PRIMARY_KEYS AS DK WHERE DK.OPERATION = 'I';
                IF @ROWCOUNT > 0
                BEGIN
                    INSERT INTO SISWEB_OWNER_SHADOW.PARTSPDF (PARTSPDF_ID, MEDIANUMBER, PDFNUMBER, PDFFILENAME, PDFTYPE, REVISION, LANGUAGEINDICATOR, FILEPATH,FILESIZE,LASTMODIFIEDDATE)
                    OUTPUT 'INSERT' ACTIONTYPE,INSERTED.PARTSPDF_ID,INSERTED.MEDIANUMBER,INSERTED.PDFNUMBER,INSERTED.PDFFILENAME,INSERTED.PDFTYPE,INSERTED.REVISION,INSERTED.
					LANGUAGEINDICATOR,INSERTED.FILEPATH,INSERTED.FILESIZE, INSERTED.LASTMODIFIEDDATE
						    INTO @MERGE_RESULTS
                            SELECT PARTSPDF_ID,MEDIANUMBER,PDFNUMBER,PDFFILENAME,PDFTYPE,REVISION,LANGUAGEINDICATOR,FILEPATH,FILESIZE,SYSUTCDATETIME() AS LASTMODIFIEDDATE
                            FROM EMP_STAGING.PARTSPDF AS DT
                            WHERE EXISTS
                                      (
                                          SELECT *
                                          FROM #DIFF_PRIMARY_KEYS AS DK
                                          WHERE DK.PARTSPDF_ID = DT.PARTSPDF_ID
                                          AND DK.OPERATION = 'I'
                                      );

                    SELECT @MERGED_ROWS = @@ROWCOUNT;

                    SET @LOGMESSAGE = FORMATMESSAGE('Inserted %s rows',CAST(@MERGED_ROWS AS VARCHAR(9)));
                    EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
                END;

				-- 7.c Update if needed
                SELECT @ROWCOUNT = COUNT(*) FROM #DIFF_PRIMARY_KEYS AS DK WHERE DK.OPERATION = 'U';
                IF @ROWCOUNT > 0
                BEGIN
                    UPDATE SISWEB_OWNER_SHADOW.PARTSPDF
                    SET MEDIANUMBER = ST.MEDIANUMBER,PDFNUMBER = ST.PDFNUMBER,PDFFILENAME = ST.PDFFILENAME,PDFTYPE = ST.PDFTYPE,REVISION = ST.REVISION,
                        LANGUAGEINDICATOR = ST.LANGUAGEINDICATOR,FILEPATH = ST.FILEPATH,FILESIZE = ST.FILESIZE, LASTMODIFIEDDATE= SYSUTCDATETIME()
                    OUTPUT 'UPDATE' ACTIONTYPE,INSERTED.PARTSPDF_ID,INSERTED.MEDIANUMBER,INSERTED.PDFNUMBER,INSERTED.PDFFILENAME,INSERTED.PDFTYPE,INSERTED.REVISION,INSERTED.
                            LANGUAGEINDICATOR,INSERTED.FILEPATH,INSERTED.FILESIZE, INSERTED.LASTMODIFIEDDATE
                    INTO @MERGE_RESULTS
                    FROM EMP_STAGING.PARTSPDF AS ST
                        JOIN SISWEB_OWNER_SHADOW.PARTSPDF AS TT ON ST.PARTSPDF_ID = TT.PARTSPDF_ID
                    WHERE EXISTS
                    (
                        SELECT *
                        FROM #DIFF_PRIMARY_KEYS AS DK
                        WHERE DK.PARTSPDF_ID = ST.PARTSPDF_ID AND
                              DK.OPERATION = 'U'
                    );
                    SELECT @MERGED_ROWS = @@ROWCOUNT;

                    SET @LOGMESSAGE = FORMATMESSAGE('Updated %s rows',CAST(@MERGED_ROWS AS VARCHAR(9)));
                    EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
                END;

				-- 8. Store current version to be used the next time
				IF @DEBUG = 'TRUE'
                    SELECT * FROM @MERGE_RESULTS AS MR;

                EXEC EMP_STAGING._setLastSynchVersion @FULLTABLENAME,@CURRENT_VERSION;

				SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',
				(
					SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE =
					'UPDATE'
					) AS Updated,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted,
					(
						SELECT MR.ACTIONTYPE,MR.PARTSPDF_ID,MR.MEDIANUMBER,MR.PDFNUMBER,MR.PDFFILENAME,MR.PDFTYPE,MR.REVISION,MR.
					LANGUAGEINDICATOR,MR.FILEPATH,MR.FILESIZE
						FROM @MERGE_RESULTS AS MR FOR JSON AUTO
					) AS Modified_Rows FOR JSON PATH,WITHOUT_ARRAY_WRAPPER
				),'Modified Rows');
                EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
            END;
        END;

		/* to be done: handle full load with MERGE */
		IF @FORCE_LOAD = 'TRUE'
		--   OR @MODIFIED_ROWS_PERCENTAGE BETWEEN-0.10 AND 0.10
        BEGIN
			SET @LOGMESSAGE = 'Executing load: Force Load = TRUE';
            EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @FORCE_LOAD;

			/* MERGE command */
            MERGE INTO SISWEB_OWNER_SHADOW.PARTSPDF tgt
            USING EMP_STAGING.PARTSPDF src
            ON src.PARTSPDF_ID = tgt.PARTSPDF_ID
            WHEN MATCHED AND EXISTS
            (
                SELECT src.PDFNUMBER,src.MEDIANUMBER,src.PDFFILENAME,src.PDFTYPE,src.REVISION,src.LANGUAGEINDICATOR,src.FILEPATH,src.FILESIZE,SYSUTCDATETIME()
                    EXCEPT
                SELECT tgt.PDFNUMBER,tgt.MEDIANUMBER, tgt.PDFFILENAME,tgt.PDFTYPE,tgt.REVISION,tgt.LANGUAGEINDICATOR,tgt.FILEPATH,tgt.FILESIZE,tgt.LASTMODIFIEDDATE
            )
                THEN UPDATE SET tgt.MEDIANUMBER=src.MEDIANUMBER,tgt.PDFNUMBER = src.PDFNUMBER,tgt.PDFFILENAME = src.PDFFILENAME,tgt.PDFTYPE = src.PDFTYPE,tgt.REVISION = src.REVISION,tgt.
                LANGUAGEINDICATOR = src.LANGUAGEINDICATOR,tgt.FILEPATH = src.FILEPATH,tgt.FILESIZE = src.FILESIZE, tgt.LASTMODIFIEDDATE = SYSUTCDATETIME()
            WHEN NOT MATCHED BY TARGET
                THEN
                INSERT(PARTSPDF_ID, MEDIANUMBER, PDFNUMBER, PDFFILENAME, PDFTYPE, REVISION, LANGUAGEINDICATOR, FILEPATH,FILESIZE,LASTMODIFIEDDATE)
                VALUES (src.PARTSPDF_ID,src.MEDIANUMBER,src.PDFNUMBER,src.PDFFILENAME,src.PDFTYPE,src.REVISION,src.LANGUAGEINDICATOR,src.FILEPATH,src.FILESIZE,SYSUTCDATETIME())
                WHEN NOT MATCHED BY SOURCE
                THEN DELETE
            OUTPUT $ACTION,COALESCE(inserted.PARTSPDF_ID,deleted.PARTSPDF_ID) PARTSPDF_ID,COALESCE(inserted.MEDIANUMBER,deleted.MEDIANUMBER) MEDIANUMBER,
            COALESCE(inserted.PDFNUMBER,deleted.PDFNUMBER) PDFNUMBER,
			COALESCE(inserted.PDFFILENAME,deleted.PDFFILENAME) PDFFILENAME,COALESCE(inserted.PDFTYPE,deleted.PDFTYPE) PDFTYPE,
            COALESCE(inserted.REVISION,deleted.REVISION) REVISION,COALESCE(inserted.LANGUAGEINDICATOR,deleted.LANGUAGEINDICATOR) LANGUAGEINDICATOR,
            COALESCE(inserted.FILEPATH,deleted.FILEPATH) FILEPATH,COALESCE(inserted.FILESIZE,deleted.FILESIZE) FILESIZE,COALESCE(inserted.LASTMODIFIEDDATE,deleted.LASTMODIFIEDDATE) LASTMODIFIEDDATE
				   INTO @MERGE_RESULTS;

			/* MERGE command */
            SELECT @MERGED_ROWS = @@ROWCOUNT;

            EXEC EMP_STAGING._setLastSynchVersion @FULLTABLENAME,@CURRENT_VERSION;

			SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',
			(
				SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE =
				'UPDATE'
				) AS Updated,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted,
				(
					SELECT MR.ACTIONTYPE,MR.PARTSPDF_ID,MR.MEDIANUMBER,MR.PDFNUMBER,MR.PDFFILENAME,MR.PDFTYPE,MR.REVISION,MR.LANGUAGEINDICATOR,MR.FILEPATH,MR.FILESIZE
					FROM @MERGE_RESULTS AS MR FOR JSON AUTO
				) AS Modified_Rows FOR JSON PATH,WITHOUT_ARRAY_WRAPPER
			),'Modified Rows');
            EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
        END;

        COMMIT;
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;
    END TRY
    BEGIN CATCH
		DECLARE @ERRORMESSAGE NVARCHAR(4000) = ERROR_MESSAGE()
			   ,@ERRORLINE    INT            = ERROR_LINE()
			   ,@ERRORNUM     INT            = ERROR_NUMBER();
		IF @@TRANCOUNT > 0
			ROLLBACK TRANSACTION;
		SET @LOGMESSAGE = FORMATMESSAGE('LINE %s: %s',CAST(@ERRORLINE AS VARCHAR(10)),CAST(@ERRORMESSAGE AS VARCHAR(4000)));
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @ERRORNUM;
        THROW;
    END CATCH;
END;
GO

