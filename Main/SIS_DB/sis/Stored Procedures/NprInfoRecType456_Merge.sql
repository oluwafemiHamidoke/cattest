
-- =============================================
-- Author:      Rishabh Khatreja
-- Create Date: 20220920
-- Description: Merge external table [sisweb_owner].[LNKNPRINFORECTYPE456] into [sis].[NprInfoRecType456]
-- =============================================
CREATE PROCEDURE [sis].[NprInfoRecType456_Merge]
(@DEBUG      BIT = 'FALSE')
AS
BEGIN
    SET NOCOUNT ON

BEGIN TRY

DECLARE @MERGED_ROWS                   INT              = 0
        ,@LOGMESSAGE                    VARCHAR(MAX);

Declare @ProcName VARCHAR(200) = OBJECT_SCHEMA_NAME(@@PROCID)+'.'+OBJECT_NAME(@@PROCID)
Declare @ProcessID uniqueidentifier = NewID()

EXEC sis.WriteLog @PROCESSID = @ProcessID,@LOGTYPE = 'Information',@NAMEOFSPROC = @ProcName,@LOGMESSAGE = 'Execution started',@DATAVALUE = NULL;

DECLARE @MERGE_RESULTS TABLE
(ACTIONTYPE         NVARCHAR(10)
,Number         VARCHAR(50) NOT NULL
);

MERGE [sis].[NprInfoRecType456] AS x
    USING [SISWEB_OWNER].[LNKNPRINFORECTYPE456] AS s
    ON (s.PRIMARYSEQNO = x.PRIMARYSEQNO and s.RECORDTYPE = x.RECORDTYPE and s.SEQUENCENUMBER = x.SEQUENCENUMBER)
    WHEN MATCHED AND EXISTS
    (
    SELECT s.COLUMNDATA, s.QUANTITYRECTYPE5, s.SERIALNUMBERSYMBOL, s.SERIALNUMBERPREFIX, s.EFFECTIVESERIALNUMBER , s.DISCONTINUEDSERIALNUMBER , s.QUANTITYRECTYPE6 , s.GROUPNUMBER, s.ENGINEERINGCHANGE, s.NPRINDICATOR
    EXCEPT
    SELECT x.COLUMNDATA, x.QUANTITYRECTYPE5, x.SERIALNUMBERSYMBOL, x.SERIALNUMBERPREFIX, x.EFFECTIVESERIALNUMBER , x.DISCONTINUEDSERIALNUMBER , x.QUANTITYRECTYPE6 , x.GROUPNUMBER, x.ENGINEERINGCHANGE, x.NPRINDICATOR
    )
    THEN
UPDATE SET COLUMNDATA = s.COLUMNDATA, QUANTITYRECTYPE5 = s.QUANTITYRECTYPE5,
    SERIALNUMBERSYMBOL = s.SERIALNUMBERSYMBOL, SERIALNUMBERPREFIX = s.SERIALNUMBERPREFIX, EFFECTIVESERIALNUMBER = s.EFFECTIVESERIALNUMBER,
    DISCONTINUEDSERIALNUMBER = s.DISCONTINUEDSERIALNUMBER, QUANTITYRECTYPE6 = s.QUANTITYRECTYPE6 , GROUPNUMBER = s.GROUPNUMBER, ENGINEERINGCHANGE = s.ENGINEERINGCHANGE, NPRINDICATOR = s.NPRINDICATOR
    WHEN NOT MATCHED BY TARGET
    THEN
INSERT(PRIMARYSEQNO, RECORDTYPE, SEQUENCENUMBER, COLUMNDATA, QUANTITYRECTYPE5, SERIALNUMBERSYMBOL, SERIALNUMBERPREFIX, EFFECTIVESERIALNUMBER , DISCONTINUEDSERIALNUMBER , QUANTITYRECTYPE6 , GROUPNUMBER, ENGINEERINGCHANGE, NPRINDICATOR)
VALUES(s.PRIMARYSEQNO, s.RECORDTYPE, s.SEQUENCENUMBER, s.COLUMNDATA, s.QUANTITYRECTYPE5, s.SERIALNUMBERSYMBOL, s.SERIALNUMBERPREFIX, s.EFFECTIVESERIALNUMBER , s.DISCONTINUEDSERIALNUMBER , s.QUANTITYRECTYPE6 , s.GROUPNUMBER, s.ENGINEERINGCHANGE, s.NPRINDICATOR)
    WHEN NOT MATCHED BY SOURCE
    THEN DELETE
OUTPUT $ACTION,
    COALESCE(inserted.PRIMARYSEQNO, deleted.PRIMARYSEQNO) Number
INTO @MERGE_RESULTS;

SELECT @MERGED_ROWS = @@ROWCOUNT;

SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',(SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted
    ,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'UPDATE') AS Updated
    ,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted
    ,(SELECT MR.ACTIONTYPE
        ,MR.Number
        FROM @MERGE_RESULTS AS MR FOR JSON AUTO) AS  Modified_Rows FOR JSON PATH
        ,WITHOUT_ARRAY_WRAPPER),'NprInfoRecType456 Modified Rows');
EXEC sis.WriteLog @PROCESSID = @ProcessID,@LOGTYPE = 'Information',@NAMEOFSPROC = @ProcName,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;

Update STATISTICS sis.NprInfoRecType456 with fullscan

    EXEC sis.WriteLog @PROCESSID = @ProcessID,@LOGTYPE = 'Information',@NAMEOFSPROC = @ProcName,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;

END TRY

BEGIN CATCH

DECLARE @ERRORMESSAGE NVARCHAR(4000) = ERROR_MESSAGE()
DECLARE @ERROELINE INT= ERROR_LINE()

SET @LOGMESSAGE = FORMATMESSAGE('LINE '+ CAST(@ERROELINE AS VARCHAR(10)) + ': ' + @ERRORMESSAGE);
EXEC sis.WriteLog @PROCESSID = @ProcessID,@LOGTYPE = 'Error',@NAMEOFSPROC = @ProcName,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = NULL;

END CATCH

END
