-- =============================================
-- Author:      Nithish
-- Create Date: 20210802
-- Description: Truncates and reloads the table SISWEB_OWNER_SHADOW.LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART
--				
-- =============================================
CREATE PROCEDURE SISWEB_OWNER_STAGING.LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART_repopulate (@DEBUG BIT = 'FALSE')
AS
BEGIN
	DECLARE @PROCNAME   VARCHAR(200)     = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
		   ,@PROCESSID  UNIQUEIDENTIFIER = NEWID()
		   ,@LOGMESSAGE VARCHAR(MAX)
		   ,@TABLENAME NVARCHAR (50)='LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART'	
		   ,@dynsql_truncatetable NVARCHAR(256)	
		   ,@dynsql_dropindex1 NVARCHAR(256)	
		   ,@dynsql_addindex1 NVARCHAR(512)	
		   ,@dynsql_dropindex2 NVARCHAR(256)	
		   ,@dynsql_addindex2 NVARCHAR(512)	
		   ,@CurrentShadowSchema NVARCHAR(256)	
		   ,@CurrentShadowTable NVARCHAR(256);

	DECLARE @ERRORMESSAGE NVARCHAR(4000)
		   ,@ERRORLINE    INT
		   ,@ERRORNUM     INT;

    BEGIN TRY -- Reloads table
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Truncating and Reloading LNKEMPPRODUCTINSTANCE_PSID',
                @DATAVALUE = NULL;

        BEGIN TRANSACTION;
  
			SELECT @CurrentShadowSchema = ltrim(rtrim(substring(base_object_name,2,charindex('].',base_object_name)-2))),	
			@CurrentShadowTable=ltrim(rtrim(substring(base_object_name,charindex('.',base_object_name)+2,len(base_object_name)-charindex('.',base_object_name)-2)))	
			FROM sys.synonyms 	
			WHERE Schema_id = schema_id('SISWEB_OWNER_SHADOW')	
			And [Name] = @TABLENAME	

			SET @dynsql_truncatetable='TRUNCATE TABLE '+QUOTENAME (@CurrentShadowSchema)+'.'+QUOTENAME(@CurrentShadowTable)	
			EXEC sp_executesql @dynsql_truncatetable	

	        DROP INDEX IF EXISTS IX_LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART_SNP_LANGUAGEINDICATOR_PRODUCTSTRUCTUREID_NUMBER ON SISWEB_OWNER_SHADOW.LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART;
		
			set @dynsql_dropindex1='DROP INDEX IF EXISTS IX_LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART_SNP_LANGUAGEINDICATOR_PRODUCTSTRUCTUREID_NUMBER ON '+QUOTENAME (@CurrentShadowSchema)+'.'+QUOTENAME(@CurrentShadowTable)+';'	
			EXEC sp_executesql @dynsql_dropindex1

			set @dynsql_dropindex2='DROP INDEX IF EXISTS NCX_LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART_SNP_LANGUAGEINDICATOR_PARENTPRODUCTSTRUCTUREID_NUMBER ON '+QUOTENAME (@CurrentShadowSchema)+'.'+QUOTENAME(@CurrentShadowTable)+';'	
			EXEC sp_executesql @dynsql_dropindex2

            INSERT INTO SISWEB_OWNER_SHADOW.LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART WITH(TABLOCK)
            SELECT DISTINCT a.EMPPRODUCTINSTANCE_ID, g.SNP, c.LANGUAGEINDICATOR, g.NUMBER, a.MEDIANUMBER, c.PARENTAGE, c.PARENTPRODUCTSTRUCTUREID, c.PRODUCTSTRUCTUREID, a.IESYSTEMCONTROLNUMBER, d.INFOTYPEID , f.IEPARTNUMBER AS PARTNUMBER, f.IEPARTNAME AS PARTNAME, f.IEPARTMODIFIER AS PARTMODIFIER, f.IECAPTION AS PARTCAPTION, f.isTopLevel, f.ORGCODE
            FROM SISWEB_OWNER_SHADOW.LNKIEPRODUCTINSTANCE a
            INNER JOIN SISWEB_OWNER_SHADOW.LNKIEPSID b ON a.IESYSTEMCONTROLNUMBER = b.IESYSTEMCONTROLNUMBER AND a.MEDIANUMBER = b.MEDIANUMBER
            INNER JOIN SISWEB_OWNER.MASPRODUCTSTRUCTURE c ON b.PSID = c.PRODUCTSTRUCTUREID
            INNER JOIN SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE g ON a.EMPPRODUCTINSTANCE_ID = g.EMPPRODUCTINSTANCE_ID
            INNER JOIN sis.vw_LNKIEINFOTYPE_Active d ON d.IESYSTEMCONTROLNUMBER = a.IESYSTEMCONTROLNUMBER
            INNER JOIN SISWEB_OWNER_SHADOW.LNKMEDIAIEPART_SNP_TOPLEVEL f ON c.PRODUCTSTRUCTUREID = f.PSID AND a.MEDIANUMBER = f.MEDIANUMBER AND d.IESYSTEMCONTROLNUMBER = f.IESYSTEMCONTROLNUMBER AND g.SNP = f.SNP

            UNION

            SELECT DISTINCT a.EMPPRODUCTINSTANCE_ID, b.SNP, d.LANGUAGEINDICATOR, b.NUMBER, e.MEDIANUMBER, d.PARENTAGE, d.PARENTPRODUCTSTRUCTUREID, d.PRODUCTSTRUCTUREID, '' AS IESYSTEMCONTROLNUMBER, 5 as INFOTYPEID , '' AS PARTNUMBER, '' AS PARTNAME, '' AS PARTMODIFIER, '' AS PARTCAPTION, 0 as ISTOPLEVEL, '' AS ORGCODE
            FROM SISWEB_OWNER_SHADOW.LNKPDFPRODUCTINSTANCE a
            INNER JOIN SISWEB_OWNER_SHADOW.EMPPRODUCTINSTANCE b ON a.EMPPRODUCTINSTANCE_ID = b.EMPPRODUCTINSTANCE_ID
            INNER JOIN SISWEB_OWNER_SHADOW.LNKPARTSPDFPSID c ON a.PARTSPDF_ID = c.PARTSPDF_ID
            INNER JOIN SISWEB_OWNER.MASPRODUCTSTRUCTURE d ON c.PSID = d.PRODUCTSTRUCTUREID
            INNER JOIN SISWEB_OWNER_SHADOW.PARTSPDF e ON a.PARTSPDF_ID = e.PARTSPDF_ID


			SET @dynsql_addindex1='CREATE NONCLUSTERED INDEX IX_LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART_SNP_LANGUAGEINDICATOR_PRODUCTSTRUCTUREID_NUMBER ON '+QUOTENAME (@CurrentShadowSchema)+'.'+QUOTENAME(@CurrentShadowTable)+' ([SNP] ASC,[LANGUAGEINDICATOR] ASC,[PRODUCTSTRUCTUREID] ASC,[NUMBER] ASC);'	
			EXEC sp_executesql @dynsql_addindex1		

			SET @dynsql_addindex2='CREATE NONCLUSTERED INDEX NCX_LNKEMPPRODUCTINSTANCE_LNKMEDIAIEPART_SNP_LANGUAGEINDICATOR_PARENTPRODUCTSTRUCTUREID_NUMBER ON '+QUOTENAME (@CurrentShadowSchema)+'.'+QUOTENAME(@CurrentShadowTable)+' ([SNP] ASC,[LANGUAGEINDICATOR] ASC,[PARENTPRODUCTSTRUCTUREID] ASC,[NUMBER] ASC) INCLUDE([MEDIANUMBER],[PARENTAGE],[PRODUCTSTRUCTUREID],[IESYSTEMCONTROLNUMBER],[INFOTYPEID],[PARTNUMBER],[PARTNAME],[PARTMODIFIER],[PARTCAPTION],[ISTOPLEVEL],[ORGCODE]);'	
			EXEC sp_executesql @dynsql_addindex2

        COMMIT;
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;
    END TRY
    BEGIN CATCH
        SET @ERRORMESSAGE = ERROR_MESSAGE();
                SET @ERRORLINE = ERROR_LINE();
                SET @ERRORNUM = ERROR_NUMBER();
                IF @@TRANCOUNT > 0
                    ROLLBACK TRANSACTION;
                SET @LOGMESSAGE = FORMATMESSAGE('LINE %s: %s',CAST(@ERRORLINE AS VARCHAR(10)),CAST(@ERRORMESSAGE AS VARCHAR(4000)));
        EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @ERRORNUM;
                THROW; --@ERRORNUM,@LOGMESSAGE,1;
    END CATCH;
END;