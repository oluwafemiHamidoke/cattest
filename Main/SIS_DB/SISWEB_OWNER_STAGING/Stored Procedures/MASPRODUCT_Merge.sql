-- =============================================
-- Author:      Davide Moraschi
-- Create Date: 20191119
-- Modify Date: 20191218 - Davide: added @DEBUG parameter and [LASTMODIFIEDDATE] column
-- Description: Conditional load from STAGING MASPRODUCT
-- =============================================
CREATE PROCEDURE SISWEB_OWNER_STAGING.MASPRODUCT_Merge
	(@FORCE_LOAD BIT = 'FALSE'
	,@DEBUG      BIT = 'FALSE') 
AS
BEGIN
	SET XACT_ABORT,NOCOUNT ON;
	BEGIN TRY
		DECLARE @MODIFIED_ROWS_PERCENTAGE      DECIMAL(12,4)    = 0
			   ,@MERGED_ROWS                   INT              = 0
			   ,@PROCNAME                      VARCHAR(200)     = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
			   ,@PROCESSID                     UNIQUEIDENTIFIER = NEWID()
			   ,@LOGMESSAGE                    VARCHAR(MAX)
			   ,@SYSUTCDATETIME                DATETIME2(6)     = SYSUTCDATETIME();
		DECLARE @MERGE_RESULTS TABLE
			(ACTIONTYPE         NVARCHAR(10)
			,PRODUCTCODE        VARCHAR(4) NOT NULL
			,SEQUENCENUMBER     NUMERIC(3,0) NOT NULL
			,LANGUAGEINDICATOR  VARCHAR(2) NOT NULL
			,PRODUCTTYPE        CHAR(1) NOT NULL
			,PRODUCTDESCRIPTION NVARCHAR(600) NOT NULL
			,FREQUENCY          CHAR(1) NULL);

		BEGIN TRANSACTION;
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution started',@DATAVALUE = NULL;

		IF @FORCE_LOAD = 'FALSE'
		BEGIN
            EXEC @MODIFIED_ROWS_PERCENTAGE = [SISWEB_OWNER_STAGING].[_getModifiedRowsPercentage] @SISWEB_OWNER_TABLE='MASPRODUCT', @SISWEB_OWNER_STAGING_TABLE = 'MASPRODUCT'
		END;

		IF
		   @FORCE_LOAD = 1
		   OR @MODIFIED_ROWS_PERCENTAGE BETWEEN 0 AND 10
		BEGIN
			SET @LOGMESSAGE = 'Executing load: ' + IIF(@FORCE_LOAD = 1,'Force Load = TRUE','Force Load = FALSE');
			EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = NULL;

			/* MERGE command */
			MERGE INTO SISWEB_OWNER.MASPRODUCT tgt
			USING SISWEB_OWNER_STAGING.MASPRODUCT src
			ON
			   src.PRODUCTCODE = tgt.PRODUCTCODE
			   AND src.SEQUENCENUMBER = tgt.SEQUENCENUMBER
			   AND src.LANGUAGEINDICATOR = tgt.LANGUAGEINDICATOR
				WHEN MATCHED AND EXISTS(SELECT src.PRODUCTTYPE
											  ,src.PRODUCTDESCRIPTION
											  ,src.FREQUENCY
										EXCEPT
										SELECT tgt.PRODUCTTYPE
											  ,tgt.PRODUCTDESCRIPTION
											  ,tgt.FREQUENCY)
				  THEN UPDATE SET tgt.PRODUCTTYPE = src.PRODUCTTYPE,tgt.PRODUCTDESCRIPTION = src.PRODUCTDESCRIPTION,tgt.FREQUENCY = src.FREQUENCY,tgt.LASTMODIFIEDDATE = @SYSUTCDATETIME
				WHEN NOT MATCHED BY TARGET
				  THEN
				  INSERT(PRODUCTCODE
						,SEQUENCENUMBER
						,LANGUAGEINDICATOR
						,PRODUCTTYPE
						,PRODUCTDESCRIPTION
						,FREQUENCY
						,LASTMODIFIEDDATE)
				  VALUES
				(src.PRODUCTCODE
				,src.SEQUENCENUMBER
				,src.LANGUAGEINDICATOR
				,src.PRODUCTTYPE
				,src.PRODUCTDESCRIPTION
				,src.FREQUENCY
				,@SYSUTCDATETIME
				) 
				WHEN NOT MATCHED BY SOURCE
				  THEN DELETE
			OUTPUT $ACTION
				  ,COALESCE(inserted.PRODUCTCODE,deleted.PRODUCTCODE)
				  ,COALESCE(inserted.SEQUENCENUMBER,deleted.SEQUENCENUMBER)
				  ,COALESCE(inserted.LANGUAGEINDICATOR,deleted.LANGUAGEINDICATOR)
				  ,COALESCE(inserted.PRODUCTTYPE,deleted.PRODUCTTYPE)
				  ,COALESCE(inserted.PRODUCTDESCRIPTION,deleted.PRODUCTDESCRIPTION)
				  ,COALESCE(inserted.FREQUENCY,deleted.FREQUENCY)
				   INTO @MERGE_RESULTS;
			/* MERGE command */

			SELECT @MERGED_ROWS = @@ROWCOUNT;
			SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',(SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted
														,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'UPDATE') AS Updated
														,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted
														,(SELECT MR.ACTIONTYPE
																,MR.PRODUCTCODE
																,MR.SEQUENCENUMBER
																,MR.LANGUAGEINDICATOR
																,MR.PRODUCTTYPE
																,MR.PRODUCTDESCRIPTION
																,MR.FREQUENCY
															FROM @MERGE_RESULTS AS MR FOR JSON AUTO) AS                                Modified_Rows FOR JSON PATH,WITHOUT_ARRAY_WRAPPER),'Modified Rows');
			EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
		END;
		ELSE
		BEGIN
			EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Skipping load: Row difference outside range (±10%)',@DATAVALUE = NULL;
		END;
		COMMIT;

								UPDATE STATISTICS SISWEB_OWNER.MASPRODUCT WITH FULLSCAN;

		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;
	END TRY
	BEGIN CATCH
		DECLARE @ERRORMESSAGE NVARCHAR(4000) = ERROR_MESSAGE()
			   ,@ERRORLINE    INT            = ERROR_LINE();

		IF @@TRANCOUNT > 0
			ROLLBACK TRANSACTION;
		SET @LOGMESSAGE = 'LINE ' + CAST(@ERRORLINE AS VARCHAR(10)) + ': ' + @ERRORMESSAGE;
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = NULL;
	END CATCH;
END;
GO

