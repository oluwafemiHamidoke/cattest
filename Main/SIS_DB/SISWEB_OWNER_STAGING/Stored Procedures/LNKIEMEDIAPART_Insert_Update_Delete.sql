-- =============================================
-- Author		: Sooraj Parameswaran
-- Create Date	: 20220518
-- Modify Date	: 
-- Description	: Conditional load from SISWEB_OWNER_STAGING.LNKIEMEDIAPART to SISWEB_OWNER.LNKIEMEDIAPART
-- =============================================

CREATE PROCEDURE [SISWEB_OWNER_STAGING].[LNKIEMEDIAPART_Insert_Update_Delete] ( @FORCE_LOAD BIT = 'FALSE'
													 			,@DEBUG      BIT = 'FALSE')
AS
BEGIN
	SET XACT_ABORT,NOCOUNT ON;
	BEGIN TRY
		DECLARE @MODIFIED_ROWS_PERCENTAGE      DECIMAL(12,4)    = 0
			   ,@MERGED_ROWS                   INT              = 0
			   ,@PROCNAME                      VARCHAR(200)     = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
			   ,@PROCESSID                     UNIQUEIDENTIFIER = NEWID()
			   ,@LOGMESSAGE                    VARCHAR(MAX)
			   ,@SYSUTCDATETIME                DATETIME2(6)     = SYSUTCDATETIME();
		DECLARE @MERGE_RESULTS TABLE (ACTIONTYPE            	NVARCHAR(10)
									, [IESYSTEMCONTROLNUMBER] 	VARCHAR(12) NOT NULL
									, [MEDIANUMBER] 			VARCHAR(8) NOT NULL
									, [SNP] 					VARCHAR(10) NULL
									, [PARTNUMBER] 				VARCHAR(20) NULL
									, [BEGINNINGRANGE] 			INT NULL
									, [ENDRANGE] 				INT NULL
									);

		BEGIN TRANSACTION;
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution started',@DATAVALUE = NULL;

		IF @FORCE_LOAD = 'FALSE'
		BEGIN
            EXEC @MODIFIED_ROWS_PERCENTAGE = [SISWEB_OWNER_STAGING].[_getModifiedRowsPercentage] @SISWEB_OWNER_TABLE='LNKIEMEDIAPART', @SISWEB_OWNER_STAGING_TABLE = 'LNKIEMEDIAPART';
		END;

		IF @FORCE_LOAD = 1 OR 
		   @MODIFIED_ROWS_PERCENTAGE BETWEEN 0 AND 10
		BEGIN
			SET @LOGMESSAGE = 'Executing load: ' + IIF(@FORCE_LOAD = 1,'Force Load = TRUE','Force Load = FALSE');
			EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = NULL;

			/* MERGE command */
			MERGE INTO SISWEB_OWNER.LNKIEMEDIAPART tgt
			USING SISWEB_OWNER_STAGING.LNKIEMEDIAPART src
			ON tgt.IESYSTEMCONTROLNUMBER = src.IESYSTEMCONTROLNUMBER
				AND tgt.SNP = src.SNP
				AND tgt.BEGINNINGRANGE = src.BEGINNINGRANGE
				AND tgt.ENDRANGE = src.ENDRANGE
				AND tgt.MEDIANUMBER = src.MEDIANUMBER
				AND tgt.PARTNUMBER = src.PARTNUMBER
			WHEN NOT MATCHED BY TARGET
				  THEN
				  INSERT(IESYSTEMCONTROLNUMBER, MEDIANUMBER, SNP, PARTNUMBER, BEGINNINGRANGE, ENDRANGE, LASTMODIFIEDDATE)
				  VALUES (src.IESYSTEMCONTROLNUMBER, src.MEDIANUMBER, src.SNP, src.PARTNUMBER, src.BEGINNINGRANGE, src.ENDRANGE, @SYSUTCDATETIME) 
			WHEN NOT MATCHED BY SOURCE
				  THEN DELETE
			OUTPUT $ACTION, 
						COALESCE(inserted.IESYSTEMCONTROLNUMBER, deleted.IESYSTEMCONTROLNUMBER) IESYSTEMCONTROLNUMBER,
						COALESCE(inserted.MEDIANUMBER, deleted.MEDIANUMBER) MEDIANUMBER,
						COALESCE(inserted.SNP, deleted.SNP) SNP,
						COALESCE(inserted.PARTNUMBER, deleted.PARTNUMBER) PARTNUMBER,
						COALESCE(inserted.BEGINNINGRANGE, deleted.BEGINNINGRANGE) BEGINNINGRANGE,
						COALESCE(inserted.ENDRANGE, deleted.ENDRANGE) ENDRANGE
				   INTO @MERGE_RESULTS;

			/* MERGE command */

			SELECT @MERGED_ROWS = @@ROWCOUNT;
			SET @LOGMESSAGE = IIF(@DEBUG = 'TRUE',
			(
				SELECT(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'INSERT') AS Inserted,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE =
				'UPDATE'
				) AS Updated,(SELECT COUNT(*) FROM @MERGE_RESULTS AS MR WHERE MR.ACTIONTYPE = 'DELETE') AS Deleted,
				(
					SELECT MR.ACTIONTYPE, MR.IESYSTEMCONTROLNUMBER, MR.MEDIANUMBER, MR.SNP, MR.PARTNUMBER, MR.BEGINNINGRANGE, MR.ENDRANGE
					FROM @MERGE_RESULTS AS MR FOR JSON AUTO
				) AS Modified_Rows FOR JSON PATH, WITHOUT_ARRAY_WRAPPER
			),'Modified Rows');
			EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = @MERGED_ROWS;
		END;
		ELSE
		BEGIN
			EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Skipping load: Row difference outside range (±10%)', @DATAVALUE = @MODIFIED_ROWS_PERCENTAGE;
		END;
		COMMIT;

		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Information',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = 'Execution completed',@DATAVALUE = NULL;
	END TRY
	BEGIN CATCH
		DECLARE @ERRORMESSAGE NVARCHAR(4000) = ERROR_MESSAGE()
			   ,@ERRORLINE    INT            = ERROR_LINE();

		IF @@TRANCOUNT > 0
			ROLLBACK TRANSACTION;
		SET @LOGMESSAGE = 'LINE ' + CAST(@ERRORLINE AS VARCHAR(10)) + ': ' + @ERRORMESSAGE;
		EXEC sis_stage.WriteLog @PROCESSID = @PROCESSID,@LOGTYPE = 'Error',@NAMEOFSPROC = @PROCNAME,@LOGMESSAGE = @LOGMESSAGE,@DATAVALUE = NULL;
		THROW;
	END CATCH;
END;
